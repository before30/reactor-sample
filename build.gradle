buildscript {
	ext {
		springBootVersion = '2.0.0.M2'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'nu.studer:gradle-jooq-plugin:2.0.6'
		classpath "gradle.plugin.com.boxfuse.client:flyway-release:4.2.0"
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'
apply plugin: "org.flywaydb.flyway"

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-jooq')
	compile('org.springframework.boot:spring-boot-starter-webflux')
	runtime('com.h2database:h2')
	compileOnly('org.projectlombok:lombok')
//	compile('com.h2database:h2:1.4.193')
	jooqRuntime 'com.h2database:h2:1.4.193'
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('io.projectreactor:reactor-test')
	compile('org.springframework.boot:spring-boot-starter-data-redis')
	compile group: 'com.google.guava', name: 'guava', version: '22.0'
	compile group: 'com.github.kstyrc', name: 'embedded-redis', version: '0.6'
	compile group: 'io.lettuce', name: 'lettuce-core', version: '5.0.0.M2'
}

jooq {
	version = '3.9.2'
	edition = 'OSS'
	sample(sourceSets.main) {
		jdbc {
			driver = 'org.h2.Driver'
			url = 'jdbc:h2:/tmp/h2.db'
			user = 'sa'
			password = ''
		}
		generator {
			name = 'org.jooq.util.DefaultGenerator'
			strategy {
				name = 'org.jooq.util.DefaultGeneratorStrategy'
			}
			database {
				name = 'org.jooq.util.h2.H2Database'
				includes = '.*'
				excludes = ''
				dateAsTimestamp = true
				inputSchema = 'PUBLIC'
			}
			generate {
				relations = true
				deprecated = false
				records = true
				immutablePojos = true
				fluentSetters = true
				pojos = true
				instanceFields = true
			}
			target {
				packageName = 'cc.before30.sample.domain.entity'
			}
		}
	}
}

flyway {
	driver = 'org.h2.Driver'
	url = 'jdbc:h2:/tmp/h2.db'
	user = 'sa'
	password = ''
	schemas = ['PUBLIC']
	locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

generateSampleJooqSchemaSource.dependsOn flywayMigrate
test.dependsOn flywayClean
